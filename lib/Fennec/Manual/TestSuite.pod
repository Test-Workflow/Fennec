=pod

=head1 NAME

Fennec::Manual::TestSuite - Quick guide to managing a test suite with L<Fennec>

=head1 DESCRIPTION

This guide will get you started writing a L<Fennec> based test suite for your
dist. The first step is to generate the boilerplate t/fennec.t file. A suitable
default can be generated using the fennec_init command within your project
directory.

=head1 GETTIN STARTED

    ~/my/project $ fennec_init

This should have created t/Fennec.t, here is an example:

    #!/usr/bin/perl
    use strict;
    use warnings;

    use Cwd;
    use Fennec::Runner;
    'Fennec::Runner'->init(
        random     => 1,
        cull_delay => .01,

        parallel_files => 2,
        parallel_tests => 2,

        Collector => 'Files',

        default_asserts   => [qw/ Core /],
        default_workflows => [qw/ Spec Case /],
        handlers          => [qw/ TAP /],
        filetypes         => [qw/ Module /],
        ignore            => undef,

        $ENV{ FENNEC_FILE } ? ( files => [ cwd() . '/' . $ENV{ FENNEC_FILE }]) : (),
        $ENV{ FENNEC_ITEM } ? ( search => $ENV{ FENNEC_ITEM }) : (),
    );

    Runner->start;

This file serves multiple purposes:

=over 4

=item Configuration file

This is where you configure the Runner than runs all the fennec tests. Other
fennec tools will look for this file and use the configuration it provides.
fennec_prove is one example of a tool that uses this config.

See the section CONFIGURATION DETAILS, or the docs for L<Fennec::Runner> for more details on arguments for Fennec::Runner->init;

=item Works with prove

You can use prove -I lib t/Fennec.t to run your tests. Module::Build and
Module::Install already know how to run Fennec tests because of this.

=item Parse environment variables

The last 2 items are for environment variables that can be used to tell Fennec
to only run a specified file/test name or line number. This way you do not have
to run the entire test suite every time.

=back

=head1 TEST FILES

L<Fennec> has a plugins system that means test files cna be anything, but the
default is perl modules placed under t/.

Lets say you have a file lib/MyPackage/MyThing.pm. Lets make a Fennec test for
it, you can name the test file anything you want, but the recommended name is
t/MyPackage/MyThing.pm. It is recommended that you mirror the layout of your
lib directory in your tests.

t/MyPackage/MyThing.pm:

    package TEST::MyPackage::MyThing;
    use strict;
    use warnings;

    use Fennec;

    # Tests can be placed outside of groups
    # (It is recommended that most if not all your tests go into groups)
    use_ok( 'MyPackage::MyThing' );

    # Define a test group:
    tests 'load MyThing' => sub {
        my $self = shift;
        require_ok MyPackage::MyThing;
    };

    # Use the Spec workflow:
    describe 'my group' => sub {
        my $self = shift;

        before_each { $self->reset };

        # 'it()' is an alias to 'tests()'
        it 'my test' => sub {
            my $self = shift;
            ok( 1, 'spec test!' );
        };

        # Nested!
        describe ...;
    };

    1;

The package can be anything except main, no 2 test files should implement the
same package, and it is recommended that your package be the same as the
package being tested with at least 1 change such as the TEST:: prefix.

You can use the 'tests' keyword to define as many test groups as you want.
Within the test groups you can use all the core asserts by default (see
L<Fennec::Assert::Core>). This list includes all functions normally exported by
L<Test::More>, L<Test::Warn>, and L<Test::Exception>.

=head1 ADVANCED USAGE

=over 4

=item Choosing assert plugins

By default L<Fennec::Assert::Core> is loaded, which in turn loads all Core
assert modules. You can specify alternate ones as well.

This will load L<Fennec::Assert::TBCore> asset libraries instead of Core
libraries. These are wrappers around L<Test::Builder> based test modules.

    use Fennec asserts => [ 'TBCore' ]

You can also directly use assert packages:

    use Fennec asserts => [];
    use Fennec::Assert::Core::More;
    use Fennec::Assert::TBCore::Exception;

=item Choosing workflow plugins

    use Fennec workflows => [ 'Spec' ]

You can also directly use workflow packages:

    use Fennec::Workflow::Spec;

=back

=head1 CONFIGURATION DETAILS

=over 4

=item random => $bool

Set to true to randomze test file order and test group order, this can be
overriden within test files.

=item cull_delay => $float

How long to wait between checking for test results. Result are generated in
other processes and must be collected regularly, this sets how often that will
occur. Higher numbers mean results are printed less often, but more at a time.

=item Collector => $name

=item default_asserts => \@names

=item default_workflows => \@names

=item handlers => \@names

=item filetypes => \@names

=item ignore => \@patterns || undef

=back

=head2 Parallelization

Note, to calculate how many processes might run at a time you use this
formula:

    $max_processes = 1 +
                    ( $parallel_files >= 2
                        ? $parallel_files
                        : 0
                    ) +
                    ( $parallel_tests
                        ? $parallel_files * $parallel_tests
                        : 0
                    );

This is because Fennec itself is a process, then it starts a process for each
test file, then it starts a process for each test group. If one of these
options is set to 1 then no new processes will be created for it. If both are
set to 1 then Fennec will only ever have one process.

=over

=item parallel_files => $int

How many files to run in parallel, do not set this too high.

=item parallel_tests => $int

How many test grousp to run in parallel, do not set this too high.

=back

=head2 Special Entities

=over 4

=item files => \@list

$ENV{ FENNEC_FILE } ? ( files => [ cwd() . '/' . $ENV{ FENNEC_FILE }]) : (),

=item item => $line_number

=item item => $group_name

$ENV{ FENNEC_ITEM } ? ( search => $ENV{ FENNEC_ITEM }) : (),

=back

=head1 SKIP AND TODO

Fennec has the concept of todo tests, tests which are expected to fail. You can
also mark groups as skip if they are really bad.

If an exception is thrown within a TODO block or group then a failing TODO
result will be generated alerting you, however it is todo and will not count as
a failure in the grand scheme.

    #!/usr/bin/perl;
    package TEST::MyTest;
    use strict;
    use warnings;

    # This will run, but failures will not count.
    tests not_yet_implemented => (
        todo => "This will fail",
        method => sub {
            my $self = shift;
            ok( 0, "Hello world" );
        },
    );

    # This will be skipped completely
    tests 'would die' => (
        skip => "This will die",
        method => sub {
            my $self = shift;
            die( "I eat you" );
        },
    );

    # You can also TODO specific asserts.
    tests 'some pass' => sub {
        ok( 1, 'pass' );
        TODO {
            ok( 0, 'fail' );
        } "This will fail, I will fix it later";
    }

    1;

=head1 EARLY VERSION WARNING

L<Fennec> is still under active development, many features are untested or even
unimplemented. Please give it a try and report any bugs or suggestions.

=head1 DOCUMENTATION

=over 4

=item QUICK START

L<Fennec::Manual::Quickstart> - Drop Fennec standalone tests into an existing
suite.

=item FENNEC BASED TEST SUITE

L<Fennec::Manual::TestSuite> - How to create a Fennec based test suite.

=item MISSION

L<Fennec::Manual::Mission> - Why does Fennec exist?

=item MANUAL

L<Fennec::Manual> - Advanced usage and extending Fennec.

=back

=head1 AUTHORS

Chad Granum L<exodist7@gmail.com>

=head1 COPYRIGHT

Copyright (C) 2010 Chad Granum

Fennec is free software; Standard perl licence.

Fennec is distributed in the hope that it will be useful, but WITHOUT
ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
FOR A PARTICULAR PURPOSE.  See the license for more details.
